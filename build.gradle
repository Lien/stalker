apply plugin: "groovy"
apply plugin: 'maven'
apply plugin: 'java'

group = "com.vandalsoftware.tools.gradle"

ext.release = has("release")
ext.localRepo = file("$buildDir/repo/")
ext.remoteRepo = file("$buildDir/repo/")

version = getVersion("0.1.0")

jar {
    baseName = "stalker"
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task publishLocal(type: Upload) {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: uri(project.ext.localRepo))
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.archives
            def repo
            if (project.ext.release) {
                repo = "$sonatypeRepo/releases"
            } else {
                repo = "$sonatypeSnapshotRepo/snapshots"
            }
            repository(url: "$repo") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.artifactId = "stalker"
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.android.tools.build:gradle:0.5.+'
    compile gradleApi()
    compile localGroovy()
}

def getVersion(String baseVersion) {
    if (project.ext.release) {
        return baseVersion
    }
    return baseVersion + '-SNAPSHOT'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}

